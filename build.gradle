plugins {
    id 'java'
    id 'application'
    id 'maven-publish'
    id 'eclipse'
    id 'jacoco'
    id 'pmd'
    id 'checkstyle'
    id 'project-report'
    id 'distribution'
    id 'build-dashboard'
    id 'com.github.spotbugs-base' version '5.2.1'
}

application {
    mainClass = 'de.tauber.gradi.Main'
}

defaultTasks 'build', 'htmlDependencyReport', 'jacocoTestReport'

java {
    //OWB 4.0.0 does not support Java 21
    sourceCompatibility = 21
    targetCompatibility = 21
    toolchain.languageVersion = JavaLanguageVersion.of(21)
}

println("group: " + group + ", project.name: " + project.name + ", version: " + version)
println("description: " + description)

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

//extra properties
ext {
}

repositories {
    mavenCentral()
    maven {
        url = 'https://repository.apache.org/content/groups/snapshots'
    }
}

configurations {
    owbTestRuntime {
        description = 'Provides owb runtime to tests'
    }
    weldTestRuntime {
        description = 'Provides weld runtime to tests'
    }
    testRuntimeOnly {
        description = 'Customized runtime for unit tests'
        //extendsFrom testImplementation, owbTestRuntime
        extendsFrom testImplementation, weldTestRuntime
    }
}

dependencies {
    //CDI API
    implementation 'jakarta.enterprise:jakarta.enterprise.cdi-api:4.1.0'
    implementation 'org.apache.openwebbeans:openwebbeans-se:4.0.3'

    //Bean Validation
    implementation 'jakarta.validation:jakarta.validation-api:3.1.1'
    testRuntimeOnly 'org.apache.bval:org.apache.bval.bundle:3.0.2'
    testRuntimeOnly 'org.apache.commons:commons-lang3:3.20.0'

    //Logging with slf4j
    implementation 'org.slf4j:slf4j-api:2.0.17'
    implementation 'org.slf4j:slf4j-simple:2.0.17'
    
    //Unit testing
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.assertj:assertj-core:3.27.6'
    testImplementation 'org.mockito:mockito-all:1.10.19'

    //Apache Deltaspike CdiCtrl
    testImplementation 'org.apache.deltaspike.modules:deltaspike-test-control-module-api:2.0.1'
    testRuntimeOnly 'org.apache.deltaspike.modules:deltaspike-test-control-module-impl:2.0.1'
    testRuntimeOnly 'org.apache.deltaspike.core:deltaspike-core-impl:2.0.1'

    //Apache OpenWebbeans CDI Container
    owbTestRuntime 'org.apache.deltaspike.cdictrl:deltaspike-cdictrl-owb:2.0.1'
    owbTestRuntime 'org.apache.openwebbeans:openwebbeans-spi:4.0.3'
    owbTestRuntime 'org.apache.openwebbeans:openwebbeans-impl:4.0.3'

    //Jakarta Annotations
    owbTestRuntime 'jakarta.annotation:jakarta.annotation-api:3.0.0'

    //JBoss Weld CDI Container
    weldTestRuntime 'org.apache.deltaspike.cdictrl:deltaspike-cdictrl-weld:2.0.1'
    weldTestRuntime 'org.jboss.weld.se:weld-se-shaded:6.0.3.Final'

    //MyFaces + Servlet API needed for clean test run with weld
    //weldTestRuntime group: 'org.apache.myfaces.core', name: 'myfaces-impl-test', version: myfacesVersion
    //weldTestRuntime group: 'org.apache.myfaces.core', name: 'myfaces-impl', version: myfacesVersion
    //weldTestRuntime group: 'org.apache.myfaces.test', name: 'myfaces-test22', version: myfacesTest22Version
    //weldTestRuntime group: 'javax.servlet', name: 'javax.servlet-api', version: servletApiVersion



}


//Standard project layout
sourceSets {
    main {
        java {
            srcDirs = ["src/main/java"]
        }
        resources {
            srcDirs = ["src/main/resources"]
        }
    }
    test {
        java {
            srcDirs = ["src/test/java"]
        }
        resources {
            srcDirs = ["src/test/resources"]
        }
    }
}

//Run tests if project has property runTests
test.onlyIf { project.hasProperty('runTests') }

//Enable/Disable unit tests in build
test.enabled = true

//Configure unit tests
test
        {
            //Copy the CDI beans.xml before executing unit tests
            doFirst {
                copy {
                    from 'build/resources/main/META-INF/beans.xml'
                    into 'build/classes/java/main/META-INF/'
                }
                copy {
                    from 'build/resources/main/META-INF/beans.xml'
                    into 'build/classes/META-INF/'
                    }
            }
            //Filter the unit tests to run
            filter {
                includeTestsMatching "de.tauber.*"
            }
            //Include/exclude test categories
            useJUnit {
                includeCategories 'de.tauber.gradi.categories.UnitTests',   \
              'de.tauber.gradi.categories.IntegrationTests'
                excludeCategories 'de.tauber.gradi.categories.SmokeTests'
            }

            testLogging {
                events "passed", "skipped", "failed"
                //,"standardOut", "standardError"
        
                exceptionFormat = 'full'
                showExceptions = true
                showCauses = true
                showStackTraces = true
                showStandardStreams = false
            }

            jvmArgs "--add-opens=java.base/java.lang=ALL-UNNAMED"
        }

javadoc.options.addStringOption('Xdoclint:none', '-quiet')

//Include a javadoc zip
tasks.register('javadocZip', Zip) {
    dependsOn javadoc
    description = 'Generate a javadoc zip file'
    group = 'Documentation'
    from javadoc.destinationDir
}

tasks.named("assemble") {
    dependsOn('javadocZip')
}
//Generate a Maven pom for project
tasks.register('pom') {
    description = 'Generates a Maven pom for project'
    group = 'Distribution'
    doLast {
        pom {
            inceptionYear = '2014'
            licenses {
                license {
                    name = "The Apache Software License, Version 2.0"
                    url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution = 'repo'
                }
            }
        }.writeTo('pom.xml')
    }
}

//Build a dashboard?
buildDashboard.onlyIf { project.hasProperty('runChecks') }

//Checkstyle configuration
checkstyleMain.onlyIf { project.hasProperty('runChecks') }
checkstyleTest.onlyIf { project.hasProperty('runChecks') }

checkstyle {
    ignoreFailures = true
    showViolations = false
    //sourceSets = [sourceSets.main, sourceSets.test]
    configFile = file("$project.projectDir/config/checkstyle/checkstyle.xml")
}

//Task to create html pages for checkstyle reports
tasks.register('checkstyleHtml') {
    dependsOn = ['checkstyleMain', 'checkstyleTest']
    description = 'Generates Checkstyle html reports'
    group = 'Verification'
    doLast {
        ant.xslt(in: checkstyleMain.reports.xml.destination,
                style: file("$project.projectDir/config/checkstyle/checkstyle-noframes-sorted.xsl"),
                out: new File(checkstyleMain.reports.xml.destination.parent, 'main.html'))
        ant.xslt(in: checkstyleTest.reports.xml.destination,
                style: file("$project.projectDir/config/checkstyle/checkstyle-noframes-sorted.xsl"),
                out: new File(checkstyleTest.reports.xml.destination.parent, 'test.html'))
    }
}

//PMD Configuration
pmdMain.onlyIf { project.hasProperty('runChecks') }
pmdTest.onlyIf { project.hasProperty('runChecks') }

tasks.withType(Pmd).configureEach {
    reports {
        html.required = true
        xml.required = true
    }
}

pmd {
    ignoreFailures = true
    sourceSets = [sourceSets.main, sourceSets.test]
    reportsDir = file("$project.buildDir/reports/pmd")
    
}


jacoco {
    reportsDirectory = layout.buildDirectory.dir('jacocoReports')
}

jacocoTestReport {
    reports {
        html.required = true
    }
}

if (project.hasProperty('runChecks')) {
    spotbugs {
        ignoreFailures = true
        effort = 'max'
        reportLevel = 'high'
    }
}


tasks.register('listTasks') {
    doLast {
        description = 'List available gradle Tasks'
        group = 'Information'
        tasks.each { task -> println(task.name + ": " + task.description) }
    }
}

//Tasks providing project information
tasks.register('listConfigurations') {
    doLast {
        description = 'List available gradle Configurations'
        group = 'Information'
        configurations.each { conf -> println conf.name }

    }
}

//Tasks providing project information
tasks.register('listSourceSets') {
    doLast {
        description = 'List available gradle SourceSets'
        group = 'Information'
        sourceSets.each { ss -> println ss.name }
    }
}

tasks.register('listDependencies') {
    doLast {
        description = 'List all dependencies for testRuntime configuration'
        group = 'Information'
        configurations.testRuntime.allDependencies.each {
            dep -> println("" + dep.group + ":" + dep.name + ":" + dep.version)
        }
    }
}

tasks.register('listFilenames') {
    doLast {
        description = 'List filename of jars used by testRuntime'
        group = 'Information'
        configurations.testRuntime.files.each { file -> println file.name }
    }
}
