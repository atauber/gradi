apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'eclipse'
apply plugin: 'jacoco'
apply plugin: 'pmd'
apply plugin: 'findbugs'
apply plugin: 'jdepend'
apply plugin: 'checkstyle'
apply plugin: 'project-report'
apply plugin: 'distribution'
apply plugin: 'build-dashboard'
apply plugin: 'project-report'

defaultTasks 'build', 'htmlDependencyReport', 'jacocoTestReport'

sourceCompatibility = 1.8
targetCompatibility = 1.8

println("group: " + group + ", project.name: " + project.name + ", version: " + version)
println("description: " + description)

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

//extra properties
ext {
}

repositories {
    mavenCentral()
}

configurations {
    weldTestRuntime{
        description 'Provides weld runtime to tests'
    }
    owbTestRuntime{
        description 'Provides owb runtime to tests'
    }
    testRuntime {
        description 'Customized runtime for unit tests'
        extendsFrom testCompile, owbTestRuntime
        //extendsFrom testCompile, weldTestRuntime
    }
}

dependencies {
    //CDI API
    compile group: 'javax.enterprise', name: 'cdi-api', version: cdiVersion
    
    //Bean Validation
    compile group: 'javax.validation', name: 'validation-api', version: beanValidationVersion
    testRuntime group: 'org.apache.bval', name: 'org.apache.bval.bundle', version: bvalVersion 
    
    //Logging with slf4j
    compile group: 'org.slf4j', name: 'slf4j-api', version: slf4jVersion
    testRuntime group: 'ch.qos.logback', name: 'logback-classic', version: logbackVersion
	
    //Unit testing
    testCompile group: 'junit', name: 'junit', version: junitVersion
    testCompile group: 'org.assertj', name: 'assertj-core', version: assertjVersion     
    testCompile group: 'org.mockito', name: 'mockito-all', version: mockitoVersion
    
    //Apache Deltaspike CdiCtrl
    testCompile group: 'org.apache.deltaspike.modules', name: 'deltaspike-test-control-module-api', version: deltaspikeVersion
    testRuntime group: 'org.apache.deltaspike.modules', name: 'deltaspike-test-control-module-impl', version: deltaspikeVersion
    
    //Apache OpenWebbeans CDI Container
    owbTestRuntime group: 'org.apache.deltaspike.cdictrl', name: 'deltaspike-cdictrl-owb', version: deltaspikeVersion
    owbTestRuntime group: 'org.apache.openwebbeans', name: 'openwebbeans-spi', version: owbVersion
    owbTestRuntime group: 'org.apache.openwebbeans', name: 'openwebbeans-impl', version: owbVersion
    
    //JBoss Weld CDI Container
    weldTestRuntime group: 'org.apache.deltaspike.cdictrl', name: 'deltaspike-cdictrl-weld', version: deltaspikeVersion
    weldTestRuntime group: 'org.jboss.weld.se', name: 'weld-se', version: weldVersion
    
    //MyFaces + Servlet API needed for clean test run with weld
    weldTestRuntime group: 'org.apache.myfaces.core', name: 'myfaces-impl-test', version: myfacesVersion
    weldTestRuntime group: 'org.apache.myfaces.core', name: 'myfaces-impl', version: myfacesVersion
    weldTestRuntime group: 'org.apache.myfaces.test', name: 'myfaces-test22', version: myfacesTest22Version
    weldTestRuntime group: 'javax.servlet', name: 'javax.servlet-api', version: servletApiVersion		   
}


//Standard project layout
sourceSets {
    main {
        java {
            srcDirs = ["src/main/java"]
        }
        resources {
            srcDirs = ["src/main/resources"]
        }
    }
    test {
        java {
            srcDirs = ["src/test/java"]
        }
        resources {
            srcDirs = ["src/test/resources"]
        }
    }	
}

//Run tests if project has property runTests
test.onlyIf { project.hasProperty('runTests') }

//Enable/Disable unit tests in build 
test.enabled = true

//Configure unit tests
test
{
    //Copy the CDI beans.xml before executing unit tests
    doFirst {
        copy {
            from 'build/resources/main/META-INF/beans.xml'
            into 'build/classes/main/META-INF/'
        }
    }
    //Filter the unit tests to run
    filter {
        includeTestsMatching "de.tauber.*"
    }
    //Include/exclude test categories
    useJUnit {
    	includeCategories 'de.tauber.gradi.categories.UnitTests', \
            'de.tauber.gradi.categories.IntegrationTests'
    	excludeCategories 'de.tauber.gradi.categories.SmokeTests'
    }
}

//Include a javadoc zip
task javadocZip(type: Zip, dependsOn: javadoc) {
    description 'Generate a javadoc zip file'
    group = 'Documentation'
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocZip
}

//Generate a Maven pom for project
task pom  {
    description 'Generates a Maven pom for project'
    group 'Distribution'
    doLast{
        pom {
            project {
                inceptionYear '2014'
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }
            }
        }.writeTo('pom.xml')
    }
}

//Build a dashboard?
buildDashboard.onlyIf { project.hasProperty('runChecks') }

//Findbugs configuration
findbugsMain.onlyIf { project.hasProperty('runChecks') }
findbugsTest.onlyIf { project.hasProperty('runChecks') }

tasks.withType(FindBugs) {
    reports {
        html {
            enabled true
        }
        xml {
            enabled false
            xml.withMessages true
    	}
    }
}

findbugs {
    //toolVersion = '2.0.1'
    //sourceSets = [sourceSets.main]
    ignoreFailures = true
    reportsDir = file("$project.buildDir/reports/findbugs")
    effort = 'max'
    reportLevel = 'high'
    //visitors = ['FindSqlInjection', 'SwitchFallthrough']
    //omitVisitors = ['FindNonShortCircuit']
    //includeFilter = file("$project.projectDir/config/findbugs/includeFilter.xml")
    //excludeFilter = file("$project.projectDir/config/findbugs/excludeFilter.xml")
}

//PMD Configuration
pmdMain.onlyIf { project.hasProperty('runChecks') }
pmdTest.onlyIf { project.hasProperty('runChecks') }

tasks.withType(Pmd) {
    reports {
    	html {
            enabled true
        }
        xml {
            enabled true
        }
    }
}

pmd {
    ignoreFailures = true
    sourceSets = [sourceSets.main,sourceSets.test]
    ruleSetFiles = files("$project.projectDir/config/pmd/rulesets/basic.xml")
    //ruleSets = ["basic", "braces", "design"]
    consoleOutput = false
    ruleSets = [
        'java-basic',
        'java-braces',
        'java-clone',
        'java-codesize',
        'java-comments',
        //'java-controversial',
        'java-design',
        'java-empty',
        'java-finalizers',
        'java-imports',
        //'java-javabeans',
        'java-junit',
        'java-logging-jakarta-commons',
        //'java-logging-java',
        'java-migrating',
        'java-naming',
        'java-optimizations',
        //'java-strictexception',
        'java-strings',
        'java-sunsecure',
        'java-typeresolution',
        'java-unnecessary',
        'java-unusedcode'           
    ]
    reportsDir = file("$project.buildDir/reports/pmd")	
}

//JDepend configuration
jdependMain.onlyIf { project.hasProperty('runChecks') }
jdependTest.onlyIf { project.hasProperty('runChecks') }

tasks.withType(JDepend) {
    reports {
        text {
            enabled true
        }
        xml {
            enabled false
        }
    }
}

jdepend {
    ignoreFailures = true
    //sourceSets = [sourceSets.main]
    reportsDir = file("$project.buildDir/reports/jdepend")    
}

//Checkstyle configuration
checkstyleMain.onlyIf { project.hasProperty('runChecks') }
checkstyleTest.onlyIf { project.hasProperty('runChecks') }

checkstyle {
    ignoreFailures = true
    showViolations = false
    //sourceSets = [sourceSets.main, sourceSets.test]        
    configFile = file("$project.projectDir/config/checkstyle/checkstyle.xml")
}

//Task to create html pages for checkstyle reports
task checkstyleHtml(dependsOn: ['checkstyleMain', 'checkstyleTest']) {
    description = 'Generates Checkstyle html reports'
    group = 'Verification'
    doLast{
        ant.xslt(in: checkstyleMain.reports.xml.destination,
            style: file("$project.projectDir/config/checkstyle/checkstyle-noframes-sorted.xsl"),
            out: new File(checkstyleMain.reports.xml.destination.parent, 'main.html'))
        ant.xslt(in: checkstyleTest.reports.xml.destination,
            style: file("$project.projectDir/config/checkstyle/checkstyle-noframes-sorted.xsl"),
            out: new File(checkstyleTest.reports.xml.destination.parent, 'test.html'))
    }
}

//Jacoco Configuration
//Generate Java Code Coverage artefacts with tests1
test {
    jacoco {
        append = false
        destinationFile = file("$project.buildDir/jacoco/jacocoTest.exec")
        classDumpFile = file("$project.buildDir/jacoco/classpathdumps")
    }    
}

jacoco {
    reportsDir = file("$project.buildDir/reports/jacoco")
}

jacocoTestReport {
    description = 'Generate Jacoco coverage reports after running tests.'
    group = 'Reporting'
    reports {
        xml{
            enabled true
            //destination "${project.buildDir}/reports/jacoco/jacoco.xml"
        }
        csv.enabled false
        html{
            enabled true
            //destination "${project.buildDir}/reports/jacoco"
        }
    }
    sourceDirectories = files(sourceSets.main.allSource.srcDirs)
    classDirectories = files(sourceSets.main.output.classesDir)

    //additionalSourceDirs = files(['test/java', 'src/java-test'])
    //additionalClassDirs = files(['build/jacoco/UT/classpathdumps/com/abc/xyz', 'build/jacoco/IT/classpathdumps/com/abc/xyz'])
    
    //sourceDirectories = files('src/main/java')
    //classDirectories = files('build/classes/main')

    //sourceDirectories = fileTree('src/main/java')
    //classDirectories = fileTree('build/classes/main')    
}

task listTasks << {
    description = 'List available gradle Tasks'
    group = 'Information'
    tasks.each { task -> println(task.name + ": " + task.description)}
}

//Tasks providing project information
task listConfigurations << {
    description = 'List available gradle Configurations'
    group = 'Information'
    configurations.each { conf -> println conf.name } 
}

//Tasks providing project information
task listSourceSets << {
    description = 'List available gradle SourceSets'
    group = 'Information'
    sourceSets.each { ss -> println ss.name } 
}

task listDependencies << {
    description = 'List all dependencies for testRuntime configuration'
    group = 'Information'
    configurations.testRuntime.allDependencies.each {
        dep -> println("" + dep.group + ":" + dep.name + ":" + dep.version)
    }
}

task listFilenames << {
    description = 'List filename of jars used by testRuntime'
    group = 'Information'
    configurations.testRuntime.files.each { file -> println file.name }    
}